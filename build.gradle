import org.ajoberstar.grgit.Grgit

import java.time.Instant

buildscript {
    ext.kotlin_version = '1.1.2-2'
    ext.spring_boot_version = '1.4.1.RELEASE'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply plugin: 'idea'

group 'ru.edustor'
version '0.7.2'

repositories {
    mavenCentral()
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

//noinspection GroovyAssignabilityCheck
jar {
    destinationDir = new File(buildDir, "/dist")
    archiveName = "edustor-pdfgen.jar"
}

ext {
    def grgit = Grgit.open()
    shortCommitId = grgit.head().getAbbreviatedId()
    commitId = grgit.head().id
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build.properties").withWriter { w ->
            def p = new Properties()
            p['edustor.build.name'] = "Edustor PDFGen"
            p['edustor.build.version'] = "${project.version}-$shortCommitId".toString()
            p['edustor.build.date'] = Instant.now().epochSecond.toString()
            p['edustor.build.commitId'] = commitId
            p['edustor.build.repo'] = "https://github.com/edustor/pdfgen"
            p.store(w, null)
        }
    }
}
processResources.finalizedBy createProperties

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile 'com.itextpdf:kernel:7.0.1'
    compile 'com.itextpdf:io:7.0.1'
    compile 'com.itextpdf:layout:7.0.1'
    compile 'com.google.zxing:javase:3.3.0'
}